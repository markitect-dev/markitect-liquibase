plugins {
    id 'buildlogic.node-conventions'
    id 'com.diffplug.spotless'
}

def ci = providers.environmentVariable('CI').present
def windows = providers.systemProperty('os.name').get().startsWithIgnoreCase('Windows')
def nodeExecutable = nodeSetup.nodeDir.file(windows ? 'node.exe' : 'bin/node')
def npmExecutable = nodeSetup.nodeDir.file(windows ? 'npm.cmd' : 'bin/npm')
def npmInstallCache = rootProject.layout.projectDirectory.dir('.gradle/spotless-npm-install-cache')
def npmrc = rootProject.file('config/spotless/.npmrc')

spotless {
    groovyGradle {
        leadingTabsToSpaces 4
        trimTrailingWhitespace()
        endWithNewline()
    }
    java {
        ratchetFrom 'origin/main'
        target 'src/**/*.java'
        licenseHeaderFile(rootProject.file('config/spotless/license-header-java'))
            .onlyIfContentMatches('(?m)^package dev\\.markitect[.;]')
        cleanthat()
            .version(libs.versions.cleanthat.get())
            .sourceCompatibility('17')
            .addMutator('SafeButNotConsensual')
            .addMutator('UnnecessarySemicolon')
            .excludeMutator('AvoidInlineConditionals')
            .excludeMutator('LambdaIsMethodReference')
            .excludeMutator('LiteralsFirstInComparisons')
            .excludeMutator('LocalVariableTypeInference')
        googleJavaFormat(libs.versions.google.java.format.get())
            .reflowLongStrings()
    }
    json {
        target(
            'src/**/*.json',
            'renovate.json5',
        )
        prettier(libs.versions.prettier.asProvider().get())
            .nodeExecutable(nodeExecutable)
            .npmExecutable(npmExecutable)
            .npmInstallCache(npmInstallCache)
            .npmrc(npmrc)
            .config(
                [
                    'printWidth': 0,
                    'singleQuote': true,
                ],
            )
    }
    format 'markdown', {
        target '*.md'
        leadingTabsToSpaces 4
        trimTrailingWhitespace()
        endWithNewline()
    }
    pom {
        target(
            'src/**/pom.xml',
            'pom.xml',
        )
        targetExclude 'src/main/resources/archetype-resources/'
        sortPom(libs.versions.sortpom.get())
            .lineSeparator('\n')
            .expandEmptyElements(false)
            .indentAttribute('schemaLocation')
            .quiet(true)
    }
    format 'properties', {
        target(
            'src/**/*.properties',
            'gradle.properties',
        )
        replaceRegex 'prePrettier', '(?m)^([ \\t]*[#!])', '# $1'
        prettier(
            [
                'prettier': libs.versions.prettier.asProvider().get(),
                'prettier-plugin-properties': libs.versions.prettier.plugin.properties.get(),
            ],
        )
            .nodeExecutable(nodeExecutable)
            .npmExecutable(npmExecutable)
            .npmInstallCache(npmInstallCache)
            .npmrc(npmrc)
            .config(
                [
                    'parser': 'dot-properties',
                    'plugins': ['prettier-plugin-properties'],
                    'keySeparator': '=',
                    'printWidth': 0,
                ],
            )
        replaceRegex 'postPrettier', '(?m)^# ', ''
    }
    format 'toml', {
        target 'gradle/**/*.toml'
        prettier(
            [
                'prettier': libs.versions.prettier.asProvider().get(),
                'prettier-plugin-toml': libs.versions.prettier.plugin.toml.get(),
            ],
        )
            .nodeExecutable(nodeExecutable)
            .npmExecutable(npmExecutable)
            .npmInstallCache(npmInstallCache)
            .npmrc(npmrc)
            .config(
                [
                    'parser': 'toml',
                    'plugins': ['prettier-plugin-toml'],
                ],
            )
    }
    format 'xml', {
        target(
            'config/**/*.xml',
            'src/**/*.xml',
        )
        targetExclude 'src/**/pom.xml'
        leadingTabsToSpaces 2
        trimTrailingWhitespace()
        endWithNewline()
    }
    yaml {
        target(
            '.github/**/*.yml',
            'src/**/*.yaml',
            'src/**/*.yml',
        )
        leadingTabsToSpaces 2
        trimTrailingWhitespace()
        endWithNewline()
    }
    format 'misc', {
        target(
            '.github/CODEOWNERS',
            'config/**/.npmrc',
            'config/**/*.txt',
            'src/main/resources/archetype-resources/**/pom.xml',
            'src/**/META-INF/services/liquibase.*',
            'src/**/META-INF/services/org.assertj.core.configuration.Configuration',
            'src/**/META-INF/spring/org.springframework.boot.autoconfigure.AutoConfiguration.imports',
            'src/**/*.csv',
            'src/**/*.txt',
            '*.txt',
            '.editorconfig',
            '.java-version',
            '.sdkmanrc',
        )
        leadingTabsToSpaces 2
        trimTrailingWhitespace()
        endWithNewline()
    }
}

['spotlessJson', 'spotlessProperties', 'spotlessToml'].each { name ->
    tasks.named(name) {
        dependsOn rootProject.tasks.nodeSetup
    }
}

if (!ci) {
    spotlessCheck.configure {
        dependsOn spotlessApply
    }
}
