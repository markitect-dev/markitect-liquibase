plugins {
    id 'buildlogic.common-conventions'
    id 'buildlogic.checkstyle-conventions'
    id 'buildlogic.forbiddenapis-conventions'
    id 'buildlogic.jacoco-conventions'
    id 'buildlogic.rewrite-conventions'
    id 'buildlogic.spotbugs-conventions'
    id 'net.ltgt.errorprone'
    id 'net.ltgt.nullaway'
    id 'java-library'
}

def ci = providers.environmentVariable('CI').present
def skipTests = providers.systemProperty('skipTests').present
def testProject = project.name.endsWith('-test')

dependencies {
    errorprone libs.com.google.errorprone.error.prone.core
    errorprone libs.com.uber.nullaway.nullaway

    annotationProcessor platform(libs.org.springframework.boot.spring.boot.dependencies)
    compileOnly libs.com.github.spotbugs.spotbugs.annotations
    compileOnly libs.de.thetaphi.forbiddenapis
    compileOnly libs.com.google.errorprone.error.prone.annotations
    compileOnly libs.org.jspecify.jspecify
    implementation platform(libs.org.springframework.boot.spring.boot.dependencies)

    testCompileOnly libs.com.github.spotbugs.spotbugs.annotations
    testCompileOnly libs.com.google.errorprone.error.prone.annotations
    testCompileOnly libs.de.thetaphi.forbiddenapis
    testCompileOnly libs.org.jspecify.jspecify
}

java {
    sourceCompatibility = 17
    toolchain {
        languageVersion = JavaLanguageVersion.of(libs.versions.java.get())
        vendor = JvmVendorSpec.AZUL
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    if (!ci) {
        outputs.upToDateWhen { false }
    }
    options.compilerArgs += '-parameters'
    if (ci) {
        options.compilerArgs += '-Werror'
    }
    options.compilerArgs += '-Xlint:all,-classfile,-processing'
    options.errorprone {
        if (!ci) {
            allDisabledChecksAsWarnings = true
            allSuggestionsAsWarnings = true
            disable 'Java8ApiChecker'
            disable 'SuppressWarningsWithoutExplanation'
            warn 'RequireExplicitNullMarking'
        }
        nullaway {
            excludedFieldAnnotations.add('org.junit.jupiter.api.io.TempDir')
            excludedFieldAnnotations.add('org.mockito.Captor')
            excludedFieldAnnotations.add('org.mockito.InjectMocks')
            excludedFieldAnnotations.add('org.mockito.Mock')
            excludedFieldAnnotations.add('org.springframework.test.context.bean.override.mockito.MockitoBean')
            warn()
        }
    }
    options.release = 17
}

testing.suites.withType(JvmTestSuite).configureEach {
    useJUnitJupiter()
    targets.all {
        testTask.configure {
            onlyIf { !skipTests }
            environment 'LIQUIBASE_ANALYTICS_ENABLED', 'false'
            environment 'LIQUIBASE_ANALYTICS_LOG_LEVEL', 'info'
            environment 'LIQUIBASE_SHOW_BANNER', 'false'
            environment 'LIQUIBASE_SQL_LOG_LEVEL', 'info'
        }
    }
}

jar.configure {
    onlyIf { !testProject }
    if (project.hasProperty('automaticModuleName')) {
        manifest {
            attributes(['Automatic-Module-Name': project.property('automaticModuleName')])
        }
    }
    metaInf {
        from rootProject.file('LICENSE.txt')
        from rootProject.file('NOTICE.txt')
    }
}

javadoc.configure {
    (options as StandardJavadocDocletOptions).addBooleanOption('Xdoclint:all,-missing', true)
}

tasks.named('javadocJar', Jar) {
    onlyIf { !testProject }
}

tasks.named('sourcesJar', Jar) {
    onlyIf { !testProject }
    metaInf {
        from rootProject.file('LICENSE.txt')
        from rootProject.file('NOTICE.txt')
    }
}

nullaway {
    onlyNullMarked = true
}
