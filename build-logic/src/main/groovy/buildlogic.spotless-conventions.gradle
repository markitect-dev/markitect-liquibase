plugins {
    id 'buildlogic.node-conventions'
    id 'com.diffplug.spotless'
}

def ci = providers.environmentVariable('CI').present
def windows = providers.systemProperty('os.name').get().startsWithIgnoreCase('Windows')
def nodeExecutable = nodeSetup.nodeDir.file(windows ? 'node.exe' : 'bin/node')
def npmExecutable = nodeSetup.nodeDir.file(windows ? 'npm.cmd' : 'bin/npm')
def npmInstallCache = rootProject.layout.projectDirectory.dir('.gradle/spotless-npm-install-cache')
def npmrc = rootProject.file('config/spotless/.npmrc')

spotless {
    ratchetFrom 'origin/main'
    plugins.withId('java') {
        groovyGradle {
            leadingTabsToSpaces 4
            trimTrailingWhitespace()
            endWithNewline()
        }
        java {
            target 'src/**/*.java'
            targetExclude 'src/it/*/src/'
            licenseHeaderFile rootProject.file('config/spotless/license-header-java')
            cleanthat()
                .version(libs.versions.cleanthat.get())
                .sourceCompatibility('17')
                .addMutator('SafeButNotConsensual')
                .addMutator('UnnecessarySemicolon')
                .excludeMutator('AvoidInlineConditionals')
                .excludeMutator('LambdaIsMethodReference')
                .excludeMutator('LiteralsFirstInComparisons')
                .excludeMutator('LocalVariableTypeInference')
            googleJavaFormat(libs.versions.google.java.format.get())
                .reflowLongStrings()
        }
    }
    json {
        target (
            'src/**/*.json',
            'renovate.json5',
        )
        targetExclude 'src/test/resources/snapshots/'
        prettier(libs.versions.prettier.asProvider().get())
            .nodeExecutable(nodeExecutable)
            .npmExecutable(npmExecutable)
            .npmInstallCache(npmInstallCache)
            .npmrc(npmrc)
            .config(
                [
                    'singleQuote': true,
                ],
            )
    }
    format 'markdown', {
        target (
            '*.md',
        )
        leadingTabsToSpaces 4
        trimTrailingWhitespace()
        endWithNewline()
    }
    format 'properties', {
        target (
            'src/**/*.properties',
            'gradle.properties',
        )
        prettier(
            [
                'prettier': libs.versions.prettier.asProvider().get(),
                'prettier-plugin-properties': libs.versions.prettier.plugin.properties.get(),
            ],
        )
            .nodeExecutable(nodeExecutable)
            .npmExecutable(npmExecutable)
            .npmInstallCache(npmInstallCache)
            .npmrc(npmrc)
            .config(
                [
                    'parser': 'dot-properties',
                    'plugins': ['prettier-plugin-properties'],
                    'keySeparator': '=',
                    'printWidth': 0,
                ],
            )
    }
    format 'toml', {
        target 'gradle/**/*.toml'
        prettier(
            [
                'prettier': libs.versions.prettier.asProvider().get(),
                'prettier-plugin-toml': libs.versions.prettier.plugin.toml.get(),
            ],
        )
            .nodeExecutable(nodeExecutable)
            .npmExecutable(npmExecutable)
            .npmInstallCache(npmInstallCache)
            .npmrc(npmrc)
            .config(
                [
                    'parser': 'toml',
                    'plugins': ['prettier-plugin-toml'],
                ],
            )
    }
    format 'xml', {
        target(
            'config/**/*.xml',
            'src/**/*.xml',
        )
        leadingTabsToSpaces 2
        trimTrailingWhitespace()
        endWithNewline()
    }
    yaml {
        target(
            '.github/**/*.yml',
            'src/**/*.yaml',
            'src/**/*.yml',
        )
        leadingTabsToSpaces 2
        trimTrailingWhitespace()
        endWithNewline()
    }
    format 'misc', {
        target(
            '.github/CODEOWNERS',
            'config/**/.npmrc',
            'config/**/*.txt',
            'src/it/*/src/**/*.java',
            'src/**/META-INF/services/liquibase.*',
            'src/**/META-INF/services/org.assertj.core.configuration.Configuration',
            'src/**/META-INF/spring/org.springframework.boot.autoconfigure.AutoConfiguration.imports',
            'src/**/*.csv',
            'src/**/*.txt',
            '*.txt',
            '.editorconfig',
            '.java-version',
            '.sdkmanrc',
        )
        leadingTabsToSpaces 2
        trimTrailingWhitespace()
        endWithNewline()
    }
}

['spotlessJson', 'spotlessProperties', 'spotlessToml'].each { name ->
    tasks.named(name) {
        dependsOn rootProject.tasks.nodeSetup
    }
}

spotlessCheck.configure {
    if (!ci) {
        dependsOn spotlessApply
    }
}
